/**
 * Created by: spurcell
 * 12/25/14
 */

main is -> {

    // Array literal (including empty), access, and cardinality
    empty = [];
    words = ["foo", "mani", "padme", "hum"];

    if #empty != 0 {
        fail "problem";
    }

    if words[2] != "padme" {
        fail "oh me oh my";
    }

    if #words != 4 {
        fail "what the deuce?!";
    }

    // Array slicing
    subset = words[1..3];

    if #subset != 3 {
        fail "darn";
    }

    if subset[0] != words[1] {
        fail "uh oh";
    }

    if subset[1] != words[2] {
        fail "how could that be?";
    }

    // Array slicing shorthand
    subset = words[0..];

    if #subset != #words {
        fail "dang";
    }

    if subset[1] != words[1] {
        fail "that's weird";
    }

    // Array excision
    snip = cut words[1];

    if snip != "mani" {
        fail "crud";
    }

    if #words != 3 {
        fail "too bad";
    }

    words = ["foo", "mani", "padme", "hum"];
    subset = cut words[1..2];

    if #subset != 2 {
        fail "that's not good";
    }

    if #words != 2 {
        fail "problem found";
    }

    if subset[0] != "mani" {
        fail "my goodness";
    }

    if words[1] != "hum" {
        fail "that's a real shame";
    }

    // Map literals (including empty), access, and cardinality

    emptyMap = {=>};

    if #emptyMap != 0 {
        fail "problem";
    }

    cities = {
        "nyc" => "New York",
        "phi" => "Philadelphia"
    };

    // todo this shouldn't work, it does incidentally
    if cities.nyc != "New York" {
        fail "something happened";
    }

    if cities["nyc"] != "New York" {
        fail "whoops";
    }

    if #cities != 2 {
        fail "what the deuce?!";
    }

    // String cardinality
    name = "snooks";

    if #name < 6 {
        fail "WHAT?!";
    }
};